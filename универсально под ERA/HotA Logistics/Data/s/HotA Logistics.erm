ZVSE

** Описание HoA Logistics

!?FU(OnAfterErmInstructions); [перед началом новой игры или после загрузки старой]
!!FU(Fun_SetDescriptionFromERT):P0/2/1/650003;[Устанавливаем базовое описание навыка]
!!FU(Fun_SetDescriptionFromERT):P0/2/2/650004;[Устанавливаем продвинутое описание навыка]
!!FU(Fun_SetDescriptionFromERT):P0/2/3/650005;[Устанавливаем экспертное описание навыка]

!?FU(OnAfterLoadGame);        [после загрузки]
!!FU(Fun_SetDescriptionFromERT):P0/2/1/650003;[Устанавливаем базовое описание навыка]
!!FU(Fun_SetDescriptionFromERT):P0/2/2/650004;[Устанавливаем продвинутое описание навыка]
!!FU(Fun_SetDescriptionFromERT):P0/2/3/650005;[Устанавливаем экспертное описание навыка]

!?FU(OnEveryDay);                       // в начале нового игрового дня (до таймеров)
!!OW:H-1/1/0; !!VRy1:Sv1;               // y1 - кол-во героев текущего игрока
!!re k/1/y1:;
  !!OW:H-1/1/k; !!VRy2:Sv1;             // y2 - номер героя, y3 - очки движения (MP)
  !!HEy2:P?y5/?y6/?y7; !!TRy5/y6/y7:T?j/?i/?i/?i/?i/?i/?i/?i; // j - тип почвы под героем (8 - вода)
  !!HEy2:W?y3/1;                        // Получаем MP

  !!HEy2:A2/70/?y8/?y9;                 // Считаем Перчатки Всадника у героя, y8 - в рюкзаке, y9 - надетые
  !!VRy3&y9>0/j<>8:-300;                // Зануляем бонус от Перчаток Всадника (+300)
  !!HEy2:A2/98/?y10/?y11;               // Считаем Сапоги-Скороходы у героя, y10 - в рюкзаке, y11 - надетые
  !!VRy3&y11>0/j<>8:-600;               // Зануляем бонус от Сапогов-Скороходов (+600)
  !!HEy2:Wy3/1 Gy3;                     // обновление MP героя
  
  !!HEy2:S2/?y4;                        // Проверяем y4 - лвл логистики
  !!VRy3&y4=1/j<>8::110 *105;           // Базовая логистика - пересчет MP с +10% до +5%
  !!VRy3&y4=2/j<>8::120 *110;           // Продвинутая логистика - пересчет MP с +20% до +10%
  !!VRy3&y4=3/j<>8::130 *120;           // Экспертная логистика - пересчет MP с +30% до +20%
  !!HEy2:Wy3/1 Gy3;                     // обновление MP героя
  
  !!VRy3&y9>0/j<>8:+200;                // Перчатки Всадника +200 MP
  !!VRy3&y11>0/j<>8:+400;               // Сапоги-Скороходы +400 MP
  !!HEy2:Wy3/1 Gy3;                     // обновление MP героя
!!en:;
**end
