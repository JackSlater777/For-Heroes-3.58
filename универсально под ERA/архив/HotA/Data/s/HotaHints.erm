ZVSE2


!?FU(gem_CreateERMHook);
!!SN:Ex1/1/4854756/(Before_CB_MSG_Hook);  // 0x4A13E4 - for common CB
!!SN:Ex1/1/4857430/(Before_CB_MSG_Hook);  // 0x4A1E56 - for Dragon Utopia
*!SN:Ex1/1/4899257/(Before_CB_MSG_Hook);  // 0x4AC1B9 - for Crypt/Derelict Ship/Shipwreck


!?FU(Before_CB_MSG_Hook);
!!FU(WOG_AdvMgr_GetMapItem):Pv998/v999/v1000/?(mapItem:y);  // mapItem:y - адрес
!!OBv998/v999/v1000:T?(objType:y) U?(objSubtype:y);

!!VR(delim:z):S^=^; // разделить для парсинга строки хинта

; z3 - Текст, получаемый по посещенному CB, при ПКМ на нём
; Последняя цифра:
; 0 - Охраняется группа (10-19) существа
; 1 - Охраняется мало (5-9) Зеленые драконы, и мало (5-9) Красные драконы, и пара (1-4) Золотые драконы, и пара (1-4) Черные драконы
!!SN&(objType)=16:E4248400/(CALLCONV_FASTCALL)/?z3/(mapItem)/(objSubtype)/i^timerOwner^/(delim:z)/1;  // Для обычных банков: 0-6 - подтип банка
*!SN&(objType)=85:E4248400/(CALLCONV_FASTCALL)/?z3/(mapItem)/7/i^timerOwner^/^^/1;  // Для кораблекрушения: 7 - подтип банка (16/7 по WOG таблице в памяти)
*!SN&(objType)=24:E4248400/(CALLCONV_FASTCALL)/?z3/(mapItem)/8/i^timerOwner^/^^/1;  // Для покинутого корабля: 8 - подтип банка (16/8 по WOG таблице в памяти)
*!SN&(objType)=84:E4248400/(CALLCONV_FASTCALL)/?z3/(mapItem)/9/i^timerOwner^/(delim:z)/1;  // Для склепа: 9 - подтип банка (16/9 по WOG таблице в памяти)
!!SN&(objType)=25:E4248400/(CALLCONV_FASTCALL)/?z3/(mapItem)/10/i^timerOwner^/(delim:z)/1;  // Для утопии: 10 - подтип банка (16/10 по WOG таблице в памяти)

;!IF:L^z3 = %z3^; // debug

; Формируем строку с названием банка (objName) из z3
!!SN:Kz3/?(lenz3:y);
!!VR(objName:z):S^^;

!!re i/0/(lenz3)/1;
  !!SN:Kz3/i/?(char:z);
  !!if&(char)<>(delim:z);
    !!VR(objName):+(char);
  !!el;
    !!VRi:+1;
    !!br;
  !!en;
!!en;

; Формируем строку с описанием охраны (guardDesc) из z3
!!SN:K(objName)/?(lenName:y);
!!VR(guardDesc:z):S^^;
!!VR(lenName):+1;

!!re j/(lenName)/(lenz3)/1;
  !!SN:Kz3/j/?(char:z);
  !!if&(char)<>(delim:z);
    !!VR(guardDesc):+(char);
  !!el;
    !!VRj:+1;
    !!br;
  !!en;
!!en;

; Добавляем к итоговому сообщению сообщение об охране
!!VR(youHaveFoundMsgPtr:z)&(objType)=16:S^%T(hota_hints.creature_banks.visiting1)^ +(objName) +^%T(hota_hints.creature_banks.visiting2)%T(wog.endl)%T(wog.endl)^ +(guardDesc);  // Для обычных банков
!!VR(youHaveFoundMsgPtr:z)&(objType)=25:S^%T(hota_hints.dragon_utopia.visiting)%T(wog.endl)%T(wog.endl)^ +(guardDesc);  // Для утопии
*!VR(youHaveFoundMsgPtr:z)&(objType)=84:S^%T(hota_hints.crypt.visiting)%T(wog.endl)%T(wog.endl)^ +(guardDesc);  // Для склепа

;!IF:M^%(youHaveFoundMsgPtr:z)^; // debug

; Помещаем итоговое сообщение в текстовый буфер
!!SN:E6388190/(CALLCONV_CDECL)/6911016/(youHaveFoundMsgPtr:z);

; Хукаем
!!UN&(objType)=16:Cx1/(STRUCT_HOOK_CONTEXT_EAX)/4/6911016;  // Обычные банки
!!UN&(objType)=25:Cx1/(STRUCT_HOOK_CONTEXT_ECX)/4/6911016;  // Утопия драконов
*!UN&(objType)=84:Cx1/(STRUCT_HOOK_CONTEXT_ECX)/4/6911016;  // Склеп
