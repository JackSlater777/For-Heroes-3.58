ZVSE

** Author orig.  : JackSlater
** Name rus.     : Доп к внешним жилищам 8 уровня

!#TM19:S1/1/1/255;            [первый день для всех игроков]

!?TM19;                       [в первый день: замена фабрик военной техники храмами]
!!UN:P137/?y1;                [проверяем включена ли опция 137 (Level 8 Creature Dwellings в y1]
!!FU&y1<>1:E;                 [выход если опция не включена]
!!UN:U14/-1/?y1;              [y1 - количество лебединых озер]
!!DO7936/1/y1/1:P;            [заменяем лебединые озера нычкой сказочных драконов]
!!UN:U28/-1/?y2;              [y2 - количество домиков фей]
!!DO7939/1/y2/1:P;            [заменяем домики фей нычкой сказочных драконов]
!!UN:U15/-1/?y3;              [y3 - количество вуалей тьмы]
!!DO7940/1/y3/1:P;            [заменяем вуали тьмы нычкой драколичей]
!!UN:U13/1/?y4;               [y4 - количество наземных картографов]
!!DO7941/1/y4/1:P;            [заменяем наземные картографы нычкой драколичей]
!!UN:U13/2/?y5;               [y4 - количество подземных картографов]
!!DO7942/1/y5/1:P;            [заменяем подземные картографы нычкой драколичей]

*!UN:P883/2;                  [отключаем опцию 883]

!?FU7936;                     [замена лебединого озера нычкой сказочных драконов]
!!UN:U14/-1/1/1;              [v1..v3 - координаты лебединого озера]
!!UN:Ov1/v2/v3/1;             [удаляем лебединые озера]
!!VRv1:+1;                    [смещаем создаваемый объект вправо на 1 клетку]
!!UN:Iv1/v2/v3/17/64/0;       [помещаем нычку сказочных драконов]

!?FU7939;                     [замена домика фей нычкой сказочных драконов]
!!UN:U28/-1/1/1;              [v1..v3 - координаты домика фей]
!!UN:Ov1/v2/v3/1;             [удаляем лебединые озера]
!!VRv1:+1;                    [смещаем создаваемый объект вправо на 1 клетку]
!!UN:Iv1/v2/v3/17/64/0;       [помещаем нычку сказочных драконов]

!?FU7940;                     [замена вуаля тьмы нычкой драколичей]
!!UN:U15/-1/1/1;              [v1..v3 - координаты вуаля тьмы]
!!UN:Ov1/v2/v3/1;             [удаляем вуаль тьмы]
!!VRv1:+1;                    [смещаем создаваемый объект вправо на 1 клетку]
!!UN:Iv1/v2/v3/17/100/0;      [помещаем нычку драколичей]

!?FU7941;                     [замена наземного картографа нычкой драколичей]
!!UN:U13/1/1/1;               [v1..v3 - координаты наземного картографа]
!!UN:Ov1/v2/v3/1;             [удаляем наземный картограф]
!!VRv1:+1;                    [смещаем создаваемый объект вправо на 1 клетку]
!!UN:Iv1/v2/v3/17/100/0;      [помещаем нычку драколичей]

!?FU7942;                     [замена подземного картографа нычкой драколичей]
!!UN:U13/2/1/1;               [v1..v3 - координаты подземного картографа]
!!UN:Ov1/v2/v3/1;             [удаляем наземный картограф]
!!VRv1:+1;                    [смещаем создаваемый объект вправо на 1 клетку]
!!UN:Iv1/v2/v3/17/100/0;      [помещаем нычку драколичей]
** end
