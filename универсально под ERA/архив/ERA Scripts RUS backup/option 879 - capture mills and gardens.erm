ZVSE
ERMS_ScriptDate=22.9(September).2012
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2004.10.5.945
** Author orig.  : DracoLich
** Name          : Capture the mills and leprechaun's gardens
** Name rus.     : Захват мельниц и садов лепрекона
** Changes rus.  : [Algor] вынос опции в отдельный файл для мода ERA, построчные комментарии
**                 [Algor] вынос текстов в ert-файл
**                 [Algor] технология быстрого поиска для UN:U
**                 [Algor] Замена TM100 на "стандартный" TM51, перенумерация функций/переменных и пр.
** Options       : 879
** Dialogs       : -
** Variables     : -
** Tmp variables : v1-v3, z1
** Timers        : TM51 (standard)
** Functions     : FU7934
** PO-values     : V0,V1 (mills and leprechaun's gardens squares)

** дублирование стандартных триггеров
!#TM51:S8/999/7/255;          [каждую неделю для всех игроков начиная с 8го дня]

!?OB109;                      [посещение Водяной мельницы игроком-человеком]
!!UN:P879/?y1;                [проверяем включена ли опция в y1]
!!FU&y1=0:E;                  [выход, если опция не включена]
!!PO998:O?y1;                 [y1 - хозяин объекта]
!!OW:C?y2;                    [y2 - текущий игрок]
!!OB998:R;                    [разрешаем посещение объекта всем игрокам]
!!PO998:Oy2;                  [меняем владельца мельницы]
!!WM998&y1=-1:B?y3 B0;        [y3 - количество золота на мельнице, если мельница ничейная]
!!VRy3&y1=-1:*500;            [...]
!!OW&y1=-1:R-1/6/dy3;         [увеличиваем игроку золото, если мельница была ничейной]
!!PO998&y1=-1:V0/6 V1/y3;     [устанавливаем тип/количество текущих ресурсов в V0/V1 значения клетки]
!!FU&-1000:E;                 [выход, если игрок ИИ]
!!OB998:S;                    [запрещаем посещение объекта всем игрокам]
!!VRz1&y1=y2:Sz179072;        [сообщение, если игрок уже получал золото]
!!IF&y1=y2:M^%Z1^;            [...]
!!VRz1&y1=-1:Sz179073;        [сообщение о захвате и получении ресурсов]
!!IF&y1=-1:Q2/6/y3/1^%Z1^;    [...]
!!VRz1&y1<>y2/y1>-1:Sz179074; [сообщение о захвате мельницы]
!!IF&y1<>y2/y1>-1:M^%Z1^;     [...]
*!PO998:N?y2;
*!PO998:N5;

!?OB112;                      [посещение Ветряной мельницы]
!!UN:P879/?y1;                [проверяем включена ли опция в y1]
!!FU&y1=0:E;                  [выход, если опция не включена]
!!PO998:O?y1;                 [y1 - хозяин объекта]
!!OW:C?y2;                    [y2 - текущий игрок]
!!OB998:R;                    [разрешаем посещение объекта всем игрокам]
!!PO998:Oy2;                  [меняем владельца мельницы]
!!ML998&y1=-1:B?y3/?y4 Bd/0;  [y3/y4 - тип/количество ресурса на мельнице, если мельница ничейная]
!!OW&y1=-1:R-1/y3/dy4;        [увеличиваем игроку ресурсы, если мельница была ничейной]
!!PO998&y1=-1:V0/y3 V1/y4;    [устанавливаем тип/количество текущих ресурсов в V0/V1 значения клетки]
!!FU&-1000:E;                 [выход, если игрок ИИ]
!!OB998:S;                    [запрещаем посещение объекта всем игрокам]
!!VRz1&y1=y2:Sz179075;        [сообщение, если игрок уже получал ресурсы]
!!IF&y1=y2:M^%Z1^;            [...]
!!VRz1&y1=-1:Sz179076;        [сообщение о захвате и получении ресурсов]
!!IF&y1=-1:Q2/y3/y4/1^%Z1^;   [...]
!!VRz1&y1<>y2/y1>-1:Sz179077; [сообщение о захвате мельницы]
!!IF&y1<>y2/y1>-1:M^%Z1^;     [...]
*!PO998:N?y2;
*!PO998:N5;

!?OB55;                       [посещение Сада лепрекона]
!!UN:P879/?y1;                [проверяем включена ли опция в y1]
!!FU&y1=0:E;                  [выход, если опция не включена]
!!PO998:O?y1;                 [y1 - хозяин объекта]
!!OW:C?y2;                    [y2 - текущий игрок]
!!OB998:R;                    [разрешаем посещение объекта всем игрокам]
!!PO998:Oy2;                  [меняем владельца мельницы]
!!GD998:B?y3 B0;              [y3/y4 - тип/количество ресурса в саду, если сад ничейный]
!!VRy4&y3<>6:S5;              [...]
!!VRy4&y3=6:S500;             [...]
!!OW&y1=-1:R-1/y3/dy4;        [увеличиваем игроку ресурсы, если сад был ничейным]
!!PO998&y1=-1:V0/y3 V1/y4;    [устанавливаем тип/количество текущих ресурсов в V0/V1 значения клетки]
!!FU&-1000:E;                 [выход, если игрок ИИ]
!!OB998:S;                    [запрещаем посещение объекта всем игрокам]
!!VRz1&y1=y2:Sz179078;        [сообщение, если игрок уже получал ресурсы]
!!IF&y1=y2:M^%Z1^;            [...]
!!VRz1&y1=-1:Sz179079;        [сообщение о захвате и получении ресурсов]
!!IF&y1=-1:Q2/y3/y4/1^%Z1^;   [...]
!!VRz1&y1<>y2/y1>-1:Sz179080; [сообщение о захвате сада]
!!IF&y1<>y2/y1>-1:M^%Z1^;     [...]
*!PO998:N?y2;
*!PO998:N5;

!?CM0;                        [информация по ПКМ на карте/объекте]
!!UN:P879/?y1;                [проверяем включена ли опция в y1]
!!CM:F?y2;                    [тип клика 512-ПКМ]
!!FU|y1=0/y2<>512:E;          [выход, если опция не включена или не ПКМ]
!!CM:P?v2/?v3/?v4;            [v2-v4 - координаты клика]
!!FU420000:P;                 [корректировка координат]
!!OB2:T?y1;                   [y1 - тип объекта]
!!FU&y1<>112/y1<>109/y1<>55:E;[выход если клик не на мельнице/садке]
!!CM:R0;                      [отключаем стандартное ПКМ-действие]
!!PO2:O?y2;                   [y2 - владелец объекта]
!!OW:C?y3;                    [y3 - текущий игрок]
!!UN:P36/?y9;                 [y9 - проверка скрипта Mithril Enhancements]
!!PO2&y9>0:N?y10;             [y10 - пользовательское число, если Mithril Enhancements активен (см. скрипт 42wog)]
!!VRz1&y2<>y3/y1=112/y2=-1:Sz179081;[сообщение "ничейная возд. мельница"]
!!VRz1&y2<>y3/y1=112/y2>-1:Sz179084;[сообщение "чужая возд. мельница"]
!!VRz1&y2=y3/y1=112:Sz179088; [сообщение "своя ветряная мельница"]
!!VRz1&y2<>y3/y1=112/y2=-1/y10<4/y10>1:Sz179651;[сообщение "ничейная улучшенная возд. мельница"]
!!VRz1&y2<>y3/y1=112/y2>-1/y10<4/y10>1:Sz179652;[сообщение "чужая улучшенная возд. мельница"]
!!VRz1&y2=y3/y1=112/y10<4/y10>1:Sz179653; [сообщение "своя улучшенная ветряная мельница"]
!!VRz1&y2<>y3/y1=109/y2=-1:Sz179082;[сообщение "ничейная водн. мельница"]
!!VRz1&y2<>y3/y1=109/y2>-1:Sz179085;[сообщение "чужая водн. мельница"]
!!VRz1&y2=y3/y1=109:Sz179650; [сообщение "своя водн. мельница"]
!!VRz1&y2<>y3/y1=109/y2=-1/y10<4/y10>1:Sz179654;[сообщение "ничейная улучшенная водн. мельница"]
!!VRz1&y2<>y3/y1=109/y2>-1/y10<4/y10>1:Sz179655;[сообщение "чужая улучшенная водн. мельница"]
!!VRz1&y2=y3/y1=109/y10<4/y10>1:Sz179656; [сообщение "своя улучшенная водн. мельница"]
!!VRz1&y2<>y3/y1=55/y2=-1:Sz179083;[сообщение "ничейный сад"]
!!VRz1&y2<>y3/y1=55/y2>-1:Sz179086;[сообщение "чужой сад"]
!!VRz1&y2=y3/y1=55:Sz179087;  [сообщение "свой сад"]
!!PO2&y2=y3:V0/?y4 V1/?y5;    [y4/y5 - тип и количество принесенных ресурсов, если объект свой]
!!IF&y2=-1:Q2/-1/-1/4/z1;     [вывод сообщения "ничейный"]
!!IF&y3<>y2/y2>-1:Q2/10/y2/4/z1;[вывод сообщения "чужой"]
!!IF&y3=y2:Q2/y4/y5/4/z1;     [вывод сообщения "свой"]

!?TM99;                       [поставка ресурсов владельцам мельниц/садов каждый первый день недели, начиная с 8го дня]
!!UN:P879/?y1;                [проверяем включена ли опция в y1]
!!FU&y1=0:E;                  [выход, если опция не включена]
!!VRy1:Sc; !!FU&y1<2:E;       [автосбор отключен в первый день]
!!VRy1:%7; !!FU&y1<>1:E;      [автосбор ресурсов по понедельникам]
!!OW:C?y1;                    [y1 - текущий игрок]
!!UN:U112/-1/?y2;             [y2 - количество Ветряных мельниц]
!!VRv1:S-1;                   [инициализация v1 для быстрого поиска]
!!DO7934/1/y2/1:Py1/112;      [для каждого объекта передаем ресурсы владельцу]
!!UN:U109/-1/?y2;             [y2 - количество Водных мельниц]
!!VRv1:S-1;                   [инициализация v1 для быстрого поиска]
!!DO7934/1/y2/1:Py1/109;      [для каждого объекта передаем ресурсы владельцу]
!!UN:U55/-1/?y2;              [y2 - количество Садов лепрекона]
!!VRv1:S-1;                   [инициализация v1 для быстрого поиска]
!!DO7934/1/y2/1:Py1/55;       [для каждого объекта передаем ресурсы владельцу]

!?FU7934;                     [x1 - текущий игрок, x2 - тип объекта]
!!UN:Ux2/-1/-1/1;             [получаем координаты объекта в v1-v3]
!!PO1:O?y1;                   [y1 - владелец объекта]
!!FU&y1<>x1:E;                [выход, если текущий игрок не владелец]
!!UN:P36/?y9;                 [y9 - проверка скрипта Mithril Enhancements]
!!PO1&y9>0:N?y10;             [y10 - пользовательское число, если Mithril Enhancements активен (см. скрипт 42wog)]
*** Ветряная мельница ***
!!ML1&x2=112:B?y2/?y3 By2/0;  [y2/y3 - тип/количество текущих ресурсов объекта. обнуляем количество]
!!VRy11&x2=112/y10<4/y10>1:S0 R3 *2;[y11 - рандом число 0 2 4 или 6 ]
!!VRy3&x2=112/y10<4/y10>1:S6 +y11;[значение ресурсов 6 8 10 или 12]
*** Водяное колесо ***
!!WM1&x2=109:B?y3 B0;         [...]
!!VRy3&x2=109:*500;           [...]
!!VRy3&x2=109/y10<4/y10>1:S2000; [2000 золота, если колесо улучшено за мифрил]
!!VRy2&x2=109:S6;             [...]
*** Мистический сад ***
!!GD1&x2=55:B?y2;             [y2 - тип текущих ресурсов объекта]
!!GD1&x2=55:T0 B0;
!!VRy3&x2=55/y2<6:S5;         [5 драг камней]
!!VRy3&x2=55/y2=6:S500;       [500 золотых]

!!PO1:V0/y2 V1/y3;            [устанавливаем тип/количество текущих ресурсов в V0/V1 значения клетки]
!!OW:Rx1/y2/dy3;              [увеличиваем ресурсы игрока]

!?FU420000;                   Координаты объекта в v2/v3/v4 обновляются на правильные (желтый квадрат). Автор: gamecreator
; v2/v3/v4 - coords
!!UN:C6919480/4/?y1;          0x699538
!!VRy1:+130112;               0x1FC40
!!UN:Cy1/4/?y2;               y2 = map tiles
!!VRy1:+4;
!!UN:Cy1/4/?y3;               y3 = map size
!!VRy5:Sv4 *y3 +v3 *y3 +v2 *38 +y2; this object, 0x26 is tile size
!!VRv2:C0/0/0;                reuse vars
**SN:E4239120/2/?v2/y5/0/0;   thiscall 0x40AF10 (map tile, screenX?, screenY?) for struct{0,0,0}
!!SN:E5230832/2/y5; !!VRv1&v1<1:Sy5; [(c) igrik]
; v1 = yellow tile
!!VRv1:-y2 :38;               object index
!!VRv2:Sv1 %y3;               x
!!VRv3:Sv1 :y3 %y3;           y
!!VRv4:Sv1 :y3 :y3;           l

** Хинт посещённого\захваченного объекта
*?FU(OnAdvMapTileHint);

*!SN:X?y1/?y2/?y3/?y4/?y5;
*!VRy10:S55;
*!VRy11:S109;
*!VRy12:S112;
*!FU&y4<>y10/y4<>y11/y4<>y12:E;[Выход если не мельницы или сад лепрекона]
*!UN:P879/?y10;                [проверяем включена ли опция в y1]
*!FU&y10=0:E;                  [выход, если опция не включена]

*!if|y4=y10/y4=y11/y4=y12;

*!POy1/y2/y3:O?y11;                   [y2 - владелец объекта]
*!OW:C?y12;                           [y3 - текущий игрок]
*!POy1/y2/y3:N?y6;                    (Check PN value 2 means has already been visited but not cleared)

*!VRz1&y6=0:S^%Z1^;
*!HTy4/-1&y6=0:P0/z1;           [Set description for never visited]

*!VRz1&y2<>y3/y6=1:S^%Z1 (Not Visited)^;     [Not visited]
*!HTy4/-1&y2<>y3/y6=1:P1/z1;

*!VRz1&y2=y3/y6=5:S^%Z1 (Visited)^;         [Visited]
*!HTy4/-1&y2=y3/y6=5:P2/z1;
*!en:;
** end
