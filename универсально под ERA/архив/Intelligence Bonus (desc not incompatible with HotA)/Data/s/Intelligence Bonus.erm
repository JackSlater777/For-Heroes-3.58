ZVSE

**Z-Vars used: z948-z950
**Function used: FU644

*?PI;
*!VRz948:Sz156 +^%Z179112^;                      [Add Skill description]
*!UN:G0/24/1/948;
*!VRz949:Sz157 +^%Z179113^;
*!UN:G0/24/2/949;
*!VRz950:Sz158 +^%Z179114^;
*!UN:G0/24/3/950;

!?FU(OnAfterErmInstructions); [перед началом новой игры или после загрузки старой]
!!FU(Fun_SetDescriptionFromERT):P0/24/1/179112;      [Устанавливаем базовое описание навыка]
!!FU(Fun_SetDescriptionFromERT):P0/24/2/179113;      [Устанавливаем продвинутое описание навыка]
!!FU(Fun_SetDescriptionFromERT):P0/24/3/179114;      [Устанавливаем экспертное описание навыка]

!?FU(OnAfterLoadGame);        [после загрузки]
!!FU(Fun_SetDescriptionFromERT):P0/24/1/179112;      [Устанавливаем базовое описание навыка]
!!FU(Fun_SetDescriptionFromERT):P0/24/2/179113;      [Устанавливаем продвинутое описание навыка]
!!FU(Fun_SetDescriptionFromERT):P0/24/3/179114;      [Устанавливаем экспертное описание навыка]

!?HL-1;                                         [Level up trigger for intelligence]

!!HE-1:S24/?y1;                                 [get intelligence level]
!!VRy2&y1>0:Sy1;                                [convert bonus to percentage - 20, 35, 50]
!!VRy2&y1>0:*15;
!!VRy2&y1>0:+5;
!!VRy3&y1>0:S0 T99;                             [roll percentile dice...]
!!FU644&y1>0/y3<y2:P;                           [handle intelligence bonus]

** End of level up trigger for intelligence


!?FU644;                                        [Function to give intelligence bonus to hero -1]

!!VRy1:S2 R1;                                   [random primary skill]
!!VRy2:S31 +y1;                                 [picture of skill]

!!HE-1&y1=2:Fd/d/d1/d;                          [give appropriate bonus]
!!HE-1&y1=3:Fd/d/d/d1;
!!HE-1:B0/?z-10;                                [get hero name]

!!VRz-9:S^With the help of Intelligence %Z-10 learns:^;[Store text in variable z-10]


!!HE-1:O?y3;                                    [get owner of this hero]
!!OW&y3<>-1:Iy3/?y4;                            [get AI (1) or human (0)]
!!VRy5:S0;                                      [assume AI]
!!VRy5&y3<>-1/y4=0:S1;                          [human if has owner and that owner is human]

!!IF&y5=1:Q2/y2/1/1/z-9;                        [If Bonus Show Picture]
