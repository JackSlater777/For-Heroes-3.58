ZVSE2

** Author orig.  : Algor
** Name          : Strong resistance
** Name rus.     : ”силенное сопротивление
** Options       : 777
** Dialogs       : -
** Variables     : z958, z994-z995

*#UN:P777/?v1;                [провер€ем включена ли опци€ 777 в y1]
*#UN&v1>0:P210/0;             [если включена, отключаем опцию —опротивление II]
*#VRv7195&v1>0:S0;            [...]


*?MR1;                        [перед расчетом сопротивлени€]
!?FU(OnMagicCorrectedResistance);
*!UN:P777/?y1;                [провер€ем включена ли опци€ 777 в y1]
*!FU&y1=0:E;                  [выход, если опци€ не включена]
!!MR:D?y1 F?y6;               [y1/y6 - базовый/конечный урон заклинани€]
!!VRy6:-y1 *-1;               [y6 - кол-во урона, поглощенное естественным и магическим сопротивлени€ми]
!!UN:C42231940/4/?y11;        [y2 - номер отр€да-цели заклинани€]
!!VRy11:+56;                  [...]
!!UN:Cy11/4/?y12;             [y12 - гекс отр€да, получающего урон]
!!FU|y12<0/y12>186:E;         [выход, если гекс за пределами пол€]
!!BU:Ey12/?y2;                [y2 - номер отр€да на позиции]
!!VRy2::21;                   [y2 - сторона отр€да-цели заклинани€]
!!BA:Hy2/?y2;                 [y2 - номер геро€-владельца]
!!FU&y2<0:E;                  [выход если геро€-владельца нет]
!!HEy2:S26/?y3;               [y3 - уровень —опротивлени€ (0..3) геро€]
!!VRy7&y3=1:+5;               // y7 - итоговый процент резиста
!!VRy7&y3=2:+10;
!!VRy7&y3=3:+20;
!!HEy2:X?y4/?y5/d/d/d/d/d;    [y4=0, y5=26 дл€ специалиста по сопротивлению]
!!HEy2&y4=0/y5=26:Ed/?y8;     // y8 - лвл геро€
!!VRy9&y4=0/y5=26/y3=1:Sy8 :4;// Ѕонус за специализацию
!!VRy9&y4=0/y5=26/y3=2:Sy8 :2;
!!VRy9&y4=0/y5=26/y3=3:Sy8;
!!VRy7&y4=0/y5=26:+y9;
!!HEy2:A2/57/d/?y4;           [y4 - кол-во надетых на геро€  олье неприступности]
!!VRy7&y4>0:+5;
!!HEy2:A2/58/d/?y4;           [y4 - кол-во надетых на геро€ ћанти€ равновеси€]
!!VRy7&y4>0:+10;
!!HEy2:A2/59/d/?y4;           [y4 - кол-во надетых на геро€ —апоги противодействи€]
!!VRy7&y4>0:+15;
!!VRy7:*y1 :100;              [y7 - количество урона поглощенного голем-like сопротивлением]
!!VRy1:-y7 -y6;               [y1 - новый конечный урон заклинани€]
!!VRy1&y1<0:S0;               [...]
!!MR:Fy1;                     [устанавливаем новый конечный урон заклинани€]

** end
