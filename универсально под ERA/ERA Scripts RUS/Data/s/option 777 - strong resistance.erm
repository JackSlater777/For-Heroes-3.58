ZVSE2
** Author orig.  : Algor
** Updated by    : Archer30
** Name          : Strong resistance
** Name rus.     : Усиленное сопротивление
** Options       : 777

; Enhanced Resistance disable Resistance II from WoG Options


!?FU(OnAfterErmInstructions);           [пост-инструкция: установка описаний]
!!UN:P777/?(enhRes:y);                  [проверяем включена ли опция 777]
!!FU&(enhRes)<>(TRUE):E;                [выход если опция не включена]

; Set up new skill descriptions
!!SN:H^secskill^/(SKILL_RESISTANCE)/(SKILL_BASIC)/?(desz:z);
!!SN:T^es.777.desc^/?(desc1:z)/^percent^/5;
!!SN:H^secskill^/(SKILL_RESISTANCE)/(SKILL_BASIC)/^%(desz)%(desc1)^;

!!SN:H^secskill^/(SKILL_RESISTANCE)/(SKILL_ADVANCED)/?(desz:z);
!!SN:T^es.777.desc^/?(desc2:z)/^percent^/10;
!!SN:H^secskill^/(SKILL_RESISTANCE)/(SKILL_ADVANCED)/^%(desz)%(desc2)^;

; Manage the percentage change in Expert Resistance
!!SN:H^secskill^/(SKILL_RESISTANCE)/(SKILL_EXPERT)/?(desc:z);

!!SN:K(desc)/?(length:y);

; Loop through the orignal skill description and look for the first "20"
!!re i/0/(length)/1/-1;
  !!SN:K(desc)/i/?(str:z);

  ; Replace the fisst "20" with "15"
  !!if&(str)=^2^;
    !!VRj:Si+1;

    !!if&j<(length);
      !!SN:K(desc)/j/?(str2:z);
      
      !!if&(str2)=^0^;    
        !!SN:K(desc)/i/^1^;
        !!SN:K(desc)/j/^5^;

        !!br;
      !!en;
    !!en;
  !!en;
!!en;

!!SN:T^es.777.desc^/?(desc3:z)/^percent^/15;
!!SN:H^secskill^/(SKILL_RESISTANCE)/(SKILL_EXPERT)/^%(desc)%(desc3)^;

; Set up new spec description
!!re i/(HERO_FIRST)/(HERO_LAST_WOG);
  !!HEi:X?(spec:y)/?(spec2:y);
  !!SN&(spec)=0/(spec2)=(SKILL_RESISTANCE):H^spec^/i/2/^%T(es.777.spec)^;
!!en;

; Set up new art descriptions
!!SN:H^art^/(ART_GARNITURE_OF_INTERFERENCE)/1/?(desc:z);
!!SN:H^art^/(ART_GARNITURE_OF_INTERFERENCE)/1/^%(desc)%(desc1)^;

!!SN:H^art^/(ART_SURCOAT_OF_COUNTERPOISE)/1/?(desc:z);
!!SN:H^art^/(ART_SURCOAT_OF_COUNTERPOISE)/1/^%(desc)%(desc2)^;

!!SN:H^art^/(ART_BOOTS_OF_POLARITY)/1/?(desc:z);
!!SN:H^art^/(ART_BOOTS_OF_POLARITY)/1/^%(desc)%(desc3)^;

// Set Expert Resistance to 15% (was 20%)
!?FU(OnStartOrLoad);
!!UN:P777/?(enhRes:y);        [проверяем включена ли опция 777]
!!FU&(enhRes)<>(TRUE):E;      [выход если опция не включена]

!!UN:C6548052/(UNC_INT)/?i^es_777_patch^;
!!VRe1:S15 :100;              [Шанс срабатывания экспертного сопротивления - 15%]
!!SN:X?(value:y) Xe1 X?(value2:y) X(value); [...]
!!UN:C6548052/(UNC_INT)/(value2); [...]

!?FU(OnGameLeave);
!!UN:P777/?(enhRes:y);        [проверяем включена ли опция 777]
!!FU&(enhRes)<>(TRUE):E;      [выход если опция не включена]

!!UN:C6548052/(UNC_INT)/i^es_777_patch^;
                
!?FU(OnMagicCorrectedResistance); [перед расчетом сопротивления]
!!UN:P777/?y1;                [проверяем включена ли опция 777 в y1]
!!FU&y1=0:E;                  [выход, если опция не включена]

!!MR:D?y1 F?y6;               [y1/y6 - базовый/конечный урон заклинания]
!!VRy6:-y1 *-1;               [y6 - кол-во урона, поглощенное естественным и магическим сопротивлениями]
!!UN:C42231940/4/?y11;        [y2 - номер отряда-цели заклинания]
!!VRy11:+56;                  [...]
!!UN:Cy11/4/?y12;             [y12 - гекс отряда, получающего урон]
!!FU|y12<0/y12>186:E;         [выход, если гекс за пределами поля]

!!BU:Ey12/?y2;                [y2 - номер отряда на позиции]
!!VRy2::21;                   [y2 - сторона отряда-цели заклинания]
!!BA:Hy2/?y2;                 [y2 - номер героя-владельца]
!!FU&y2<0:E;                  [выход если героя-владельца нет]

!!HEy2:S26/?y3;               [y3 - уровень Сопротивления (0..3) героя]
!!VRy7&y3=1:+5;               // y7 - итоговый процент резиста
!!VRy7&y3=2:+10;
!!VRy7&y3=3:+15;
!!HEy2:X?y4/?y5/d/d/d/d/d;    [y4=0, y5=26 для специалиста по сопротивлению]
!!HEy2&y4=0/y5=26:Ed/?y8;     // y8 - лвл героя
!!VRy9&y4=0/y5=26/y3=1:Sy8 :4; // Бонус за специализацию - базовое сопротивление +0.25% * лвл
!!VRy9&y4=0/y5=26/y3=2:Sy8 :2; // Бонус за специализацию - продвинутое сопротивление +0.5% * лвл
!!VRy9&y4=0/y5=26/y3=3:Sy8 *3 :4;    // Бонус за специализацию - экспертное сопротивление +0.75% * лвл
!!VRy7&y4=0/y5=26:+y9;         // Плюсуем бонус за специализацию к общему проценту
!!HEy2:A2/57/d/?y4;           [y4 - кол-во надетых на героя Колье неприступности]
!!VRy7&y4>0:+5;               // +5% за надетый артефакт
!!HEy2:A2/58/d/?y4;           [y4 - кол-во надетых на героя Мантия равновесия]
!!VRy7&y4>0:+10;              // +10% за надетый артефакт
!!HEy2:A2/59/d/?y4;           [y4 - кол-во надетых на героя Сапоги противодействия]
!!VRy7&y4>0:+15;              // +15% за надетый артефакт
!!VRy7:*y1 :100;              [y7 - количество урона поглощенного голем-like сопротивлением]
!!VRy1:-y7 -y6;               [y1 - новый конечный урон заклинания]
!!VRy1&y1<0:S0;               [...]
!!MR:Fy1;                     [устанавливаем новый конечный урон заклинания]

** end
