ZVSE2


!#DC(WOG_OPT_DWELLING_HINT_TEXT) = 024;


!?FU(OnAdventureMapRightMouseClick);
!!UN:P(WOG_OPT_DWELLING_HINT_TEXT)/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;
; get click params
!!CM:F?(flags:y) I?(itemID:y);
!!FU|(flags)<>(MOUSE_FLAG_RMB)/(itemID)<>37:E;
; get click coordinates
!!CM:P?(x:y)/?(y:y)/?(z:y);
; get object params
!!OB(x)/(y)/(z):T?(objectType:y) U?(dwellingType:y);
; exit if not a dwelling
!!FU&(objectType)<>(OBJ_CREATURE_GENERATOR_1)/(objectType)<>(OBJ_CREATURE_GENERATOR_4):E;
; get object entrance coordinates
!!SN:O?(x)/?(y)/?(z);
; get dwelling owner
!!DW(x)/(y)/(z):O?(owner:y)/1;
!!FU&(owner)=(NO_OWNER):E;
; get me player id
!!FU(WOG_GameMgr_GetPlayer_Me):P?(mePlayerId:y);
; get my team id
!!FU(WOG_GameMgr_GetPlayer_Team):P(mePlayerId)/?(myTeam:y);
; get dwelling owner team id
!!FU(WOG_GameMgr_GetPlayer_Team):P(owner)/?(ownerTeam:y);
; check is this my team dwelling
!!FU&(myTeam)<>(ownerTeam):E;
; disable standard action
!!CM:R(FALSE);
; get dwelling name
!!FU(WOG_GameMgr_GetDwellingName):P(objectType)/(dwellingType);
!!VR(dwellingName:z):Ss^result^;
; get owner text
!!FU(WOG_GameMgr_GetObjOwnerStr):P(owner);
!!VR(ownerText:z):Ss^result^;
; init y-vars
!#VA(monType[4]:y) (monCount[4]:y) (monName[4]:z);
!#VA(msgMonType[4]:y) (msgType[4]:y);
!!VR(msgMonType[0]):C(NO_MON)/(NO_MON)/(NO_MON)/(NO_MON);
!!VR(msgType[0]):C(NO_MON)/(NO_MON)/(NO_MON)/(NO_MON);
!!re i/0/4/1/-1:;
  !!VR(monTypePtr:y):S(@monType) +i;
  !!VR(monCountPtr:y):S(@monCount) +i;
  !!VR(monNamePtr:y):S(@monName) +i;
  !!VR(msgTypePtr:y):S(@msgType) +i;
  !!VR(msgMonTypePtr:y):S(@msgMonType) +i;
  !!DW(x)/(y)/(z):Mi/?y(monTypePtr)/?y(monCountPtr);
  !!if&y(monTypePtr)<>(NO_MON):;
    !!SN&y(monCountPtr)=1:H^monname^/y(monTypePtr)/0/?z(monNamePtr);
    !!SN&y(monCountPtr)<>1:H^monname^/y(monTypePtr)/1/?z(monNamePtr);
    !!VRy(msgTypePtr):S(PIC_TYPE_MONSTER) |(PIC_TYPE_FLAG_DISPLAY_ZEROS);
    !!VRy(msgMonTypePtr):Sy(monCountPtr) Sd<<16 |y(monTypePtr);
  !!en;
!!en:;
!!IF:N(msgType[0])/(msgMonType[0])/(msgType[1])/(msgMonType[1])/(msgType[2])/(msgMonType[2])/(msgType[3])/(msgMonType[3]);
!!IF:N(MSG_TYPE_POPUP)/^{%(dwellingName)}
%(ownerText)^;

**end
