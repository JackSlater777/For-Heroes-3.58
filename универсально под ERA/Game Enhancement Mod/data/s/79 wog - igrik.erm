ZVSE2
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2005.9.7.814

  79 wog - igrik.erm
  version 1.32 - 08.08.2020
  authors: igrik, daemon_n, PerryR
*!VRs^ddd_daemonName^:S^DIMA^;
*!SN:W^ddd_daemonName^/^DIMA^; тоже работает
*!VRs^ddd_daemonName^:S^DIMA ^ +^KOLMOGOROV^; выражения работают
s^...имя...^ — глобальная строковая переменная
i^...имя...^ — глобальная числовая переменная
SN:W сама определяет, строковая или числовая по второму аргументу
 key functions 
*?FU(gem_StartGame);
*?FU(gem_CreateERMHook);
*?FU(gem_CheckGameLang);
*?FU(gem_Autobattle_Check_And_Sound);



*#UN:P185/0;                            [отключаем меню отключения сообщений]

; --------------------------------------------------------------------------------------------------
; --------------------------------------------------------------------------------------------------
*                       [Скрипты для битвы - scripts for the battles]
; --------------------------------------------------------------------------------------------------
; --------------------------------------------------------------------------------------------------
*?FU(OnAdventureMapLeftMouseClick);
*!VRz-3:S^artifact.def^;
*!VRz-2:S^123^;
*!IF:Y?y1/?z-2/-3/?y2;
*!IF:L^%Y1/%Z-2/%Y2^;
*!IF:Y<тип_клика>/z<текст>/z<название_дефа>/<номер_кадра_дефа>;
 "W" на КП: вкл/откл режима Быстрая Битва / switch autocombat by "W" on the map (by igrik)

; --------------------------------------------------------------------------------------------------

; --------------------------------------------------------------------------------------------------
*?BR1;
*!SN:W^gem_SHOW_COMBAT_MOUSE_HEX_OPT^/?y1;
*!UN:C6916112/4/y1;
; --------------------------------------------------------------------------------------------------
** Оковы Войны (в бою с нейтралами можно убежать с поля боя)
** Shackles of war (retreat vs neutrals bttles with SW)
!?BG0; !!BG:A?y1; !!BA:H1/?y2;          [узнать героя защитника]
!!UN&y1=4/y2=-2:C4689325/1/2;           [подменить номер арта]
!?BA52;  !!UN:C4689325/1/125;           [вернуть номер арта]

; --------------------------------------------------------------------------------------------------
** Быстрая тактическая расстановка / Fast tactic troop replacement (by igrik)
!?CM4;
!!SN:L^HD_WOG.dll^/?y1 L^HD_MCR.dll^/?y2;
!!BU:T?y3;
!!FU|y1<>0/y2<>0/y3<>1:E;
!!CM:F?y2 I?y3 D?y4;
!!FU|y3>10/y2<>0/y4<0/y4>186:E;
!!BU:Ey4/?y5;
!!FU&y5=-1:E;
!!BG:N?y6 Q?y7;
!!BMy5:I?y8 T?y9;
!!FU&y9>144/y9<150:E;
!!FU&y7<>y8|y6=y5:E;
!!VRy5&y7=1:-21;
!!CM:R0;
!!UN:C6919200/4/?y1;
!!SN:E4607760/2/y1/y8/y5;
** открытие окон героев в битве по ПКМ на дефе героев (by igrik)
** open heroscreens in the vattle by right click
!?CM4;
!!SN:W^gem_option5^/?y1; 
!!BA:E?y2;
!!FU|y1<>1/y2=1:E;
!!CM:F?y1 D?y2;
!!FU&y1<>512:E;
!!FU&y2<252|y3>253:E;
!!BA:H0/?y4 H1/?y5;
!!SN&y2=252:E5118576/3/y4/1/1;
!!SN&y2=253/y5>-1:E5118576/3/y5/1/1;
!!CM:R0;
; --------------------------------------------------------------------------------------------------
** Кнопка "Ждать Всеми" / "all wait" button (by totkotoriy)
!?FU87008&x1=18;
*!IF:M^%X1^;
!!BG:Q?y1; проверяем активную сторону
      !!SN:W^isWaitSide_%Y1^/?y2;
      !!FU&y2=1:E;
      ; проверка на фазу ожидания
      !!UN:C6919200/4/?y10;
      !!VRy11:Sy10 +81380;
      !!UN:Cy11/1/?y12; Проверка на фазу ожидания: 0-НЕТ, 1=ДА
      !!FU&y12=1:E;     выход, если фаза ожидания
      ; создаем цикл прохода по стекам
      !!VRy3:S21*y1;
      !!VRy4:Sy3 +20;
      !!DO830023/y3/y4/1:P;
      !!BG:A8;
      *!SN:W^isWaitSide_%Y1^/1 W^gem_CheckGameLang^/?y5 T^%Y5_gem_battle.AllwaitDescr1^/?z-1;
      *!IF:L^%Z-1^;
!?BR; в начале каждого раунда обнуляем переменные
!!SN:W^isWaitSide_0^/0;
!!SN:W^isWaitSide_1^/0;
*?FU(OnBattleMouseHint);
*!BA:E?y2;
*!CM:I?y1;
*!SN:W^Allwait_Button_ID^/?y3;
*!FU|y1<>y3/y2=1:E;
*!SN:W^gem_CheckGameLang^/?y5 T^%Y5_gem_battle.AllwaitDescr^/?z-1;
*!MM:Mz-1;

; --------------------------------------------------------------------------------------------------
** Скрипт убирает все WoG способности, получаемые войсками при накоплении опыта - исключая статы
** script removes all WoG abilities gained by troops from their experience - excluding stats (by igrik)
!?PI;
!!FU(WogScriptsModCheck):P; !!FU&-1:E;
!!UN:P890/?y1 P742/?y2;
!!FU&y1<>1|y2=1:E;
!!UN:J11/1;
!!VRy2:S0;
!!UN:C5093986/4/?y3; [y3 кол-во существ в игре]
[:LoopNoWoGAbillity]
!!EAy2:B6/0/////////////;
!!EAy2:B7/0/////////////;
!!EAy2:B8/0/////////////;
!!EAy2:B9/0/////////////;
!!EAy2:B10/0/////////////;
!!EAy2:B11/0/////////////;
!!EAy2:B12/0/////////////;
!!EAy2:B13/0/////////////;
!!EAy2:B14/0/////////////;
!!EAy2:B15/0/////////////;
!!EAy2:B16/0/////////////;
!!EAy2:B17/0/////////////;
!!EAy2:B18/0/////////////;
!!EAy2:B19/0/////////////;
!!VRy2:+1;
!!FU&y2>1000:E;
!!SN&y2<y3:G[LoopNoWoGAbillity];
!!FU:E;
; --------------------------------------------------------------------------------------------------
**Magogs Fireball-Attack doesnt damage own troops (by PerryR)
** Магоги не атакуют собзные войска ни атакаой, ни абилкой от опыта монстров
!?MF1;
!!SN:W^Magog_Fireball_Enable^/?y1;
!!FU&y1=0:E;
!!BG:N?y1;                              [Get attacking stack number]
!!MF:N?y4;                              [Get defending stack number]
!!BMy1:I?y5;                            [Get attacking stack side]
!!BMy4:I?y6;                            [Get defending stack side]
!!UN:C42149568/4/?y10;                  [Judgement basis (028326c0)]
!!MF&y10=4454752/y5=y6:E0;              [Disable taking damage if Magog Fireball and own team]

!?MR2;                                  [Magogs Fireball-Cast that comes with Stack Expierence doesnt damage own troops]
!!SN:W^Magog_Fireball_Enable^/?y1;
!!FU&y1=0:E;
!!BG:A?y1;                              [Aktion in y1]
!!if|y1=7/y1=6:;                        [if ranged or melee attack]
!!MR:S?y2 F?y3;
!!if&y2=21/y3<=105:;                    [Fireball always does 105 damage]
!!MR:N?y10;
!!BMy10:I?y11;
!!MR&y11=0:F100;                        [Disable Magic damage for attacker side from Fireball]
!!en;
!!en;

!?FU(OnBattleRegeneratePhase);
!!SN:W^Magog_Fireball_Switch^/-1;
!!BA:Q?y1;
!!FU&y1=1:E;                            [Exit in Quick Combat]
!!SN:X?y1;                              [Event parameter]
!!BMy1:T?y2;                            [Acting Monster Type]
!!SN&y2=45:W^Magog_Fireball_Switch^/y1;

!?CM4;
!!CM:F?y1 I?y2;
!!FU|y1<>512/y2<>2010:E;                [Exit if not rightclick on defend Icon]
!!SN:W^Magog_Fireball_Switch^/?y3;
!!FU&y3=-1:E;
!!BMy3:T?y4;                            [Acting Monster Type]
!!if&y4=45:;
!!SN:W^gem_CheckGameLang^/?y5;
*!if&y1=1:;
  !!SN:T^%Y5_gem_battle.magog1^/?z1;
  !!SN:T^%Y5_gem_battle.magog2^/?z2;
  !!SN:T^%Y5_gem_battle.magog3^/?z3;
*!el:;
  *!SN:T^gem_battle_eng.magog1^/?z1;
  *!SN:T^gem_battle_eng.magog2^/?z2;
  *!SN:T^gem_battle_eng.magog3^/?z3;
*!en;
  *!VRz1:Sz187011;
  *!VRz2:Sz187012;
  *!VRz3:Sz187013;
  !!VRv3:S0;
!!IF:G1/3/0/1/2/3/0/0/0/0/0/0/0/0/0/;
!!SN&v3=1:W^Magog_Fireball_Enable^/0;
!!SN&v3=2:W^Magog_Fireball_Enable^/1;
!!en:;

!?BA0;
!!SN:W^Magog_Fireball_Enable^/1;
* end
; --------------------------------------------------------------------------------------------------
Fix the Luck and Morale bug from 8th level creatures in WoG (by PerryR)
*Устаналивает корректные бонусы/штрафы морали и удачи за т8 существ

!?FU(OnSetupBattlefield)&1000; 
!!BA:Q?f; !!FU&f=1:E;
!!BA:H0/?y1;                            [Attacking Hero]
!!BA:H1/?y2;                            [Defending Hero]
!!FU(Set_Morale_and_Luck):Py1/y2/0;

!?FU(OnCombatRound)&1000;                                                       
!!BA:Q?f; !!FU&f=1:E;
!!BA:H0/?y1;                            [Attacking Hero]
!!BA:H1/?y2;                            [Defending Hero]
!!FU(Set_Morale_and_Luck):Py1/y2/1;

!?FU(OnAfterBattleAction)&1000;
!!BA:Q?f; !!FU&f=1:E;
!!BA:H0/?y1;                            [Attacking Hero]
!!BA:H1/?y2;                            [Defending Hero]
!!FU(Set_Morale_and_Luck):Py1/y2/1;

!?FU(Set_Morale_and_Luck); 
x1=Attacker Hero, x2=Defender Hero, x3=battle state 0=BF and 1=BG and BR

Attacker

!!re i/0/10/1:;                                                                                                       
  !!BMi:T?y3;                           [Get Creature Type]
  !!if&y3=150:;                         [if 150  SupremeArchangel  found]
    *!HEx1&x3=0:R0/d1; add 1 temporary morale
    !!br:;
   !!en:;                              
!!en:;

!!re i/0/10/1:;
  !!BMi:T?y3;                                  [Get Creature Type]
  !!if&y3=153:;                                [if 153  Antichrist  found]
    !!HEx2&x3=0/x2>=0/x2<=155:R1/d-1;          [add -1 temporary Luck]
    !!DO(Reduce_Luck_Fix)/21/31/1&x2=-2/x3=1:P; 
    !!br:;
  !!en:;
!!en:;

!!re i/0/10/1:;
  !!BMi:T?y3; Get Creature Type 
  !!if&y3=154:;                                   [if 154  Blood Dragon  found]
    *!HEx2&x3=0/x2>=0/x2<=155:R0/d-1; add -2 temporary morale
    !!DO(Reduce_Morale_Fix)/21/31/1&x3=1/x2=-2:P; [Reduce Morale only if no enemy hero]
    !!br:;
  !!en:;
!!en:;

!!re i/0/10/1:;
  !!BMi:T?y3; Get Creature Type 
  !!if&y3=156:; if 156  Ghost Behemoth found
    *!HEx1&x3=0/x2>=0/x2<=155:R1/d1; add 1 temporary Luck
    !!br:;
  !!en:;
!!en:;


Defender

!!re i/21/31/1:;
  !!BMi:T?y3;                                      [Get Creature Type]
  !!if&y3=150:;                                    [if 150  SupremeArchangel  found]
    *!HEx2&x3=0/x2>=0/x2<=155:R0/d1; add 1 temporary morale
    *!HEx2&x3=0/x2=-2:R0/d1; add 1 temporary morale for neutrals without Hero at Defending side
    !!DO(Increase_Morale_Fix)/21/31/1&x2=-2/x3=1:P; 
    !!br:;
  !!en:;
!!en:;

!!re i/21/31/1:;
!!BMi:T?y3;                                        [Get Creature Type]
  !!if&y3=153:;                                    [if 153  Antichrist  found]
    !!HEx1&x3=0/x2>=0/x2<=155:R1/d-1;              [add -1 temporary Luck]
    !!br:;
  !!en:;
!!en:;

!!re i/21/31/1:;
  !!BMi:T?y3; Get Creature Type 
  !!if&y3=154:; if 154  Blood Dragon  found
    *!HEx1&x3=0/x2>=0/x2<=155:R0/d-2; add -2 temporary morale
    !!br:;
    !!en:;
  !!en:;
!!re i/21/31/1:;
  !!BMi:T?y3;                                    [Get Creature Type]
    !!if&y3=156:;                                [if 156  Ghost Behemoth found]
    !!HEx2&x3=0/x2>=0/x2<=155:R1/d2;             [add 2 temporary Luck]
    !!DO(Increase_Luck_Fix)/21/31/1&x2=-2/x3=1:P; 
    !!br:;
  !!en:;
!!en:;

!?FU(Reduce_Luck_Fix);   
!!BMx16:G213/d-1/d;                              [213 Luck]

!?FU(Increase_Luck_Fix);   
!!BMx16:G213/d2/d;                               [213 Luck]

!?FU(Reduce_Morale_Fix); 
!!BMx16:F?i;                                     [read flags]
!!VRi:&131072;                                   [just look at waiting bit]
!!BMx16&i=0:G212/d-2/d;                          [212 Moral]

!?FU(Increase_Morale_Fix); 
!!BMx16:F?i;                                     [read flags]
!!VRi:&131072;                                   [just look at waiting bit]
!!BMx16&i=0:G212/d1/d;                           [212 Moral]
; --------------------------------------------------------------------------------------------------
; --------------------------------------------------------------------------------------------------
*                 [Скрипты для карты приключений  - scripts for adventure map]
; --------------------------------------------------------------------------------------------------
; --------------------------------------------------------------------------------------------------
** скрипт, отображающий расстояние до конца маршрута, срабатывает на клик ПКМ+Alt: (by igrik)
** script that displays the distance to the end of the route is triggered by Alt + RBM on the map:
!?CM0;
!!OW:A-1/?y1;
!!CM:I?y2 F?y3 S?y4;
!!VRy5:S0;
!!VRy5&y1>=0/y1<=155/y2=37/y3=512/y4=14:S1;
!!FU&y5<>1:E;
!!SN:L^user32.dll^/?y91 Ay91/^GetKeyState^/?y92 Ey92/1/18;
!!VRy93:Sv1&65535;   !!FU&y93<32768:E; !!CM:R0; [if Alt]
!!HEy1:W?y11 B0/?z1;
!!CM:P?y1/?y2/?y3;                      [координаты]
!!UN:X?y4/?y5;                          [размер карты]
!!VRy9:Sy4 *30 *y4 *2 *y3;              [смещение для подвала]
!!VRy4: *y2 +y1;
!!VRy4: *30;                            [смещение от начала таблицы]
!!UN:C6918868/4/?y6;                    [6992D4]
!!VRy6:+36;
!!UN:Cy6/4/?y7;                         [начало таблицы]
!!VRy9:+y4 +y7 +24;
!!UN:Cy9/2/?y8;
!!VRy10:Sy11-y8;
!!SN:W^gem_CheckGameLang^/?y5;
    !!SN&y8<1:T^%Y5_gem_map.route_1^/?z2/^hero_name^/z1/^path_lenght^/y8/^hero_movepoints^/y10;
    !!SN&y8>=1/y10<0:T^%Y5_gem_map.route_2^/?z2/^hero_name^/z1^path_lenght^/y8/^hero_movepoints^/y10;
    !!SN&y8>=1/y10>=0:T^%Y5_gem_map.route_3^/?z2/^hero_name^/z1^path_lenght^/y8/^hero_movepoints^/y10;  
!!IF:M0/4/z2;         !!SN:Q;
; --------------------------------------------------------------------------------------------------

** Script to select (make active) Hero on Adventure Map by Shift+Leftclick on Hero* (by PerryR)
** by Shift+Alt+Leftclick delete at the one moment (by daemon_n)
** Взять под управление своего героя Shif + ЛКМ на нём, Удалить моментально на Shift+ALt+ЛКМ
!?CM5;
!!CM:F?y6 S?y2;
!!FU&y6<>1/y6<>33/y2<>14:E;
!!CM:P?y1/?y2/?y3;                      [Get courser position]
!!OBy1/y2/y3:T?y4; !!FU&y4<>34:E;       [Get Type of Object]
!!HEy1/y2/y3:N?y5 O?y4;                 [If Hero type get Hero Number]
!!OW:C?y1 A-1/?y2 Gy1/?y3;
!!if&y3=0:;
  !!SN:W^gem_CheckGameLang^/?y7;
  !!SN:T^%Y7_gem_map.herotake^/?z-1;
  !!IF:M1/^%Z-1^;z187014;
  !!FU:E;
!!en;


!4!IF:L^%Y1/%Y2/%Y3/%Y4/%Y5^;
!!FU|y2=y5/y1<>y4/y5<0/y5>155:E;        [If Hero and valid Hero Number]

!!if&y6=1:;
  !!re i/0/7;
  !!OW:O-1/i/?y4;
  !!if&y4=y5:;
    !!VRy1:Si+39;
    !!CM:Iy1;
    !!br:;
  !!en;
  !!en;
!!en;

!!if&y6=33:;
  !!re i/0/7:;
  !!OW:O-1/i/?y4;
  !!if&y4=y5:;
    !!HEy5:B0/?z1;
    !!CM:R0;
    !!SN:W^gem_CheckGameLang^/?y7;
    !!SN:T^%Y7_gem_map.herokill^/?z-1/^hero_name^/z1;
    !!IF:Q1/^%Z-1^;
    !!HEy5&1:K;
    !!br:;
  !!en;
  !!en:;
!!en;
!!FU:E; 
; --------------------------------------------------------------------------------------------------



; --------------------------------------------------------------------------------------------------

; --------------------------------------------------------------------------------------------------
* Часы реального времени (автор: feanor)
* Скрипт показывает реальное время и уровень заряда батареи (для ноутбуков)
* по клику правой кнопкой мыши на картинке даты.
* Show current time and laptop battery charge level by rightclick on game date * (by feanor)
!?CM0;
!!CM:I?y1 F?y2;
!!if&y1=1008/y2=512:;
!!CM:R0;
!!VRz1:S^kernel32.dll^;
!!SN:Lz1/?y2;
!!VRz1:S^GetSystemPowerStatus^;
!!SN:Ay2/z1/?y3;
!!SN:Ey3/1/?y4;
!!VRy10:Sy4;
!!VRy10::65536;
!!VRz1:S^GetLocalTime^;
!!SN:Ay2/z1/?y3;
!!SN:Ey3/1/?y4;
!!VRy8:Sy6;
!!VRy9:Sy6;
!!VRy8::65536;
!!VRy9:%65536;
!!SN:W^gem_CheckGameLang^/?y5;
!!SN:T^%Y5_gem_map.time1^/?z-1;/^hour^/y9/^minute^/y8;
!!SN:T^%Y5_gem_map.time2^/?z-2;/^effect^/y10;
!!IF&y10<>255/y9>9/y8>9:M0/4/^%Z-1 %Y9:%Y8

%Z-2 %Y10%%^;
!!IF&y10=255/y9>9/y8>9:M0/4/^%Z-1 %Y9:%Y8^;
!!IF&y10<>255/y9<10/y8>9:M0/4/^%Z-1 0%Y9:%Y8

%Z-2 %Y10%%^;
!!IF&y10=255/y9<10/y8>9:M0/4/^%Z-1  0%Y9:%Y8^;;
!!IF&y10<>255/y9<10/y8<10:M0/4/^%Z-1 0%Y9:0%Y8

%Z-2 %Y10%%^;
!!IF&y10=255/y9<10/y8<10:M0/4/^%Z-1 0%Y9:0%Y8^;
!!IF&y10<>255/y9>9/y8<10:M0/4/^%Z-1 %Y9:0%Y8

%Z-2 %Y10%%^; 
!!IF&y10=255/y9>9/y8<10:M0/4/^%Z-1 %Y9:0%Y8^;
!!en:;

; --------------------------------------------------------------------------------------------------
; --------------------------------------------------------------------------------------------------
* --------------------------------------  Всякое остальное  ----------------------------------------
; --------------------------------------------------------------------------------------------------
; --------------------------------------------------------------------------------------------------
; Быстро снять одно Знамя Полоководца со стека и положить в рюкзак по CTRL+ПКМ (by igrik)

!?FU208001;  проверка на клик "me"
!!IF:V115/0;
!!OW:C?y1;
!!OW:Gy1/?y2;
!!IF&y2=1:V115/1;

!?CM2;
!!FU208001:P0;                          // проверка на того кто кликает
!!FU&-115:E;                            // выход
!!CM:A?y1/?y2;                          // координаты клика
!!CM:H?y3/?y4;                          // номер героя
!!CM:I?y5;                              // место клика
!!CM:F?y6 I?y7;                         // тип и подтип клика
!!VRy9:Sy5-68;                          // смещение в слоты
!!FU208003&y5=141/y6=4/y3>-1:Py3;       // настройка знамени
!!FU|y5<68/y5>74:E;                     // выход
!!FU208002&y6=512:Py3/y9;               // ф-ция удаления знамени

!?FU208002;                             // ПКМ в окне героя  (положить знамя в рюкзак)
!!SN:L^user32.dll^/?y1 Ay1/^GetKeyState^/?y2 Ey2/1/17;
!!VRy1:Sv1&65535;                       // проверить на CTRL+ПКМ
!!FU&y1<32768:E;
!!HEx1:C0/x2/?y2/?y3;
!!FU&y2=-1:E;
!!EXx1/x2:R?y4/156/?y5/?y6;
!!if&y4=1:;
  !!CM:R0;
  !!EXx1/x2&y6=0:R0/156/y5/0;
  !!EXx1/x2&y6<>0:R1/156/y5/d-1;
  !!HEx1:A156;
  !!UN:R3/-1;
  !!VRz1:S^Button.wav^;
  !!SN:Pz1;
!!en:;

; --------------------------------------------------------------------------------------------------
; --------------------------------------------------------------------------------------------------
; ПКМ описание кнопок Журнал и Рюкзак (by igrik)
!?CM2; !!CM:F?y1 I?y2;
!!if&y1=512/y2=8000:;
  !!SN:W^gem_CheckGameLang^/?y5;
  !!SN:T^%Y5_gem_hero.backpack^/?z-1;
  !!CM:R0;
  !!IF:M0/4/^%Z-1^;
!!en;

!!if&y1=512/y2=128:;
  !!SN:W^gem_CheckGameLang^/?y5;
  !!SN:T^%Y5_gem_hero.questlog^/?z-1;
  !!CM:R0;
  !!IF&y1=512/y2=128:M0/4/^%Z-1^;
!!en;

;-------------------------------------------------------------------------------
!?FU87000;
!!UN:C6052891/1/4;                      [центрируем текст названия города в окне города (id 149)]


; --------------------------------------------------------------------------------------------------
; ----------------- скажем так: тут системные функции, не требующие вмешательства ------------------
; --------------------------------------------------------------------------------------------------
****************************************************************************************************
UNIVERSAL START TRIGGER by igrik

!?PI;  !!FU(gem_StartGame):P0; 
!!FU(gem_CheckGameLang):P0; 
*!FU(WogScriptsModCheck):P; *!FU&1:E; *!FU(WOG_StartGame):P0;
!?GM0; !!FU(gem_StartGame):P1; 
!!FU(gem_CheckGameLang):P1;
*!FU(WogScriptsModCheck):P; *!FU&1:E; *!FU(WOG_StartGame):P1;
!?FU(gem_CheckGameLang);
!!SN:W^gem_CheckGameLang^/0;
!!UN:J8/1/^era rus.pac^; !!SN&1:W^gem_CheckGameLang^/1; !!VRy1&1:S1;
!!SN:L^Wogcn.dll^/?y2;
!!SN&y2<>0:W^gem_CheckGameLang^/2;

!?FU(gem_StartGame);
!!SN:L^EraPlugins\erm_hooker.era^/?y1;
!!FU&y1=0:E; !!SN:Ay1/^SetHook^/?y2;
!!FU(gem_CreateERMHook):Py2;
!!SN:F^GetButtonID^/^allwait^;
!!SN:W^Allwait_Button_ID^/v1;
; радус открытия всей карты
!!UN:C5196631/4/320;                      [чит-меню (ориг = 180)]
!!UN:C4204282/4/360;                      [чит wogeyeofsauron (ориг = 200)]
; радус закрытия всей карты
!!UN:C4204369/4/360;                      [чит wogeyeofsauron (ориг = 200)]
!!FU(gem_CheckGameLang):P;
!!SN:W^gem_CheckGameLang^/?y1;
!?FU(gem_CreateERMHook);                  [функция хуков (x1 - адрес установки хука)\]
!!SN:Ex1/1/4230103/87021;                 [нажатие клавиши на КП]
!!SN:Ex1/1/4674071/87022;                 [нажатие клавиши в бою]
!!FU(WogScriptsModCheck):P; !!FU&1:E;
!!SN:Ex1/1/5013149/(WOG_OnNewDay);        [trigger on start every day]
!!SN:Ex1/1/5014608/(WOG_OnNewWeek);       [trigger on start every week]
!!SN:Ex1/1/5016587/(WOG_OnNewMonth);      [trigger on start every month]
!!SN:Ex1/1/7710213/(WOG_PreBeforeBattle); [use only in exceptional cases]
!!SN:Ex1/1/5008601/(WOG_EndOfTurn);       [trigger on every player end of turn]


!?FU(WogScriptsModCheck);
!!UN:J8/1/^wog scripts.pac^;  // отключаем повторную выдачу хуков
*!IF:L^%1^;

*?FU(gem_StartGame);


; для переигрываемой битвы (функции 870520 и 870530 вызываются из плагина BattleReplay.dll)
; но в них нельзя трогать никакие переменные, иначе будет писец
!?BA52;                                 [запустится только один раз перед всеми битвами-переигровками]
!?FU870520; !!FU87052:P;                [перед каждой ПЕРЕИГРОВКОЙ]
!?FU870530; !!FU87053:P;                [после каждой ПЕРЕИГРОВКИ]
!?BA53;                                 [запустится только один раз после всех битв-переигровок]

!?FU87021;                              [нажатие клавиши на КП]
!!SN:X?y1;
!!VRy2:Sy1 +8;  !!UN:Cy2/4/?y3;
!!VRy4:Sy3 +8;  !!UN:Cy4/4/?y5;
!!VRy6:Sy5 +4;  !!UN:Cy6/4/?y7;
*!IF:L^Клавиша: %Y7^;
!!FU87007:Py7;                          [нажатие клавиши на КП]

!?FU87022;                              [нажатие клавиши в бою]
!!SN:X?y1;
!!VRy2:Sy1 +28;  !!UN:Cy2/4/?y8;
*!IF:L^Клавиша: %Y8^;
!!FU87008:Py8;                          [нажатие клавиши в бою]
!!FU:E;
** end igrik script-79.wog

********************************************************************************
another battle features
********************************************************************************
;-------------------------------------------------------------------------------
