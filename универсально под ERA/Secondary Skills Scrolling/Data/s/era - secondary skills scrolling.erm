ZVSE

** Прокрутка вторичных навыков
** Идея и оригинальный скрипт - Berserker
** Переработка для поддержки мода "10 Вторичных навыков" от igrik'а - Algor

!#UN:P182/0;                            // отключение WoG-опции 182 "Показ скрытых навыков" за ненадобностью

!?FU(OnAfterLoadGame);                  // после загрузки игры 
!!UN:P182/0;                            // отключение WoG-опции 182 "Показ скрытых навыков" за ненадобностью

!?FU(OnPreHeroScreen);                  // перед открытием экрана героя
!!SN:W^sss.click^/?y1;                  // y1 - признак обновления окна героя после клика на кнопках прокрутки втор.навыков
!!VRy2:S0;                              // по-умолчанию открывается первая страница втор.навыков
!!SN&y1=1:W^sss.page^/?y2;              // установка текущей страницы втор.навыков, если триггер срабатывает при смене страницы
!!SN:W^sss.page^/y2;                    // корректировка номера страницы втор.навыков
!!FU(sss.ShowSkillPage):Py2;            // отображение первой страницы втор.навыков при открытии окна

!?FU(OnPostHeroScreen);                 // перед закрытием экрана героя
!!SN:W^sss.click^/?y1;                  // y1 - признак обновления окна героя после клика на кнопках прокрутки втор.навыков
!!VRy2:S0;                              // по-умолчанию открывается первая страница втор.навыков
!!SN&y1=1:W^sss.page^/?y2;              // установка текущей страницы втор.навыков, если триггер срабатывает при смене страницы
!!SN:W^sss.page^/y2;                    // корректировка номера страницы втор.навыков
!!FU(sss.ShowSkillPage):Py2;            // отображение первой страницы втор.навыков

!?FU(OnHeroScreenMouseClick);           // при клике в окне героя
!!CM:I?y1 S?y2 F?y3;                    // y1-y3 - паарметры клика
!!SN:F^GetButtonID^/^sss_next^; !!VRy4:Sv1; // y4 - ID кнопки "sss.next"
!!FU|y1<>y4/y2<>13/y3<>0:E;             // выход, если не отпущена ЛКМ на кнопке прокрутки втор.навыков
!!SN:W^sss.page^/d1 W^sss.page^/?y6;    // y6 - номер страницы втор.навыков для отображения
!!FU(sss.ShowSkillPage):Py6;            // отображение страницы
!!SN:W^sss.click^/1;                    // обход показа первой страницы при обновлении окна героя
!!UN:R3/-1;                             // обновление окна текущего героя
!!SN:W^sss.click^/0;                    // обход показа первой страницы при обновлении окна героя

!?FU(OnKeyPressed);                     // принажатии клавиши
!!SN:X?y1; !!FU|y1<33/y1>34:E;          // выход, если не PageUp/PageDown
!!SN:L^Era.dll^/?y2; !!SN:Ay2/^GetGameState^/?y3; !!SN:Ey3/1/?y4; y5 - ID текущего диалога
!!FU&y5<>5117840:E;                     // выход, если нажатие не в окне героя
!!VRy1:-33 *2 -1;                       // y1 = -1/+1 для PageUp/PageDown
!!SN:W^sss.page^/dy1 W^sss.page^/?y6;   // y6 - номер страницы втор.навыков для отображения
!!FU(sss.ShowSkillPage):Py6;            // отображение страницы
!!SN:W^sss.click^/1;                    // обход показа первой страницы при обновлении окна героя
!!UN:R3/-1;                             // обновление окна текущего героя
!!SN:W^sss.click^/0;                    // обход показа первой страницы при обновлении окна героя

!?FU(sss.ShowSkillPage);                // отображение x1 страницы втор.навыков в экране героя
** x1 - номер страницы (страницы переключаются циклически)
!!re i/0/27:;
!!HE-1:Si/?y1;
!!VRy2:+y1;
!!en;
!!FU&y2=0:E;
!!SN:M-1/28/0/0; !!VRy1:Sv1;            // y1 - временный массив страниц втор.навыков
!!VRy5:S0;                              // y5 - кол-во втор.навыков у героя/номер текущего элемента в массивах
!!re y3/0/27:;                          // заполнение массива страниц втор.навыков
  !!HE-1:Sy3/?y4;                  
  !!if&y4>0:;                           // если навык имеется
    !!SN:My1/y5/y3;                     // заполнение массива страниц втор.навыков
    !!VRy5:+1;                          // увеличение кол-а втор. навыков у героя
  !!en:;                                // 
!!en:;                                  // 
!!UN:C7620583/1/?y6;                    // y6 - максимальное количество втор.навыков отображаемых в окне героя
!!VRy8:Sy5 %y6;                         // y8 - кол-во навыков на последней странице
!!VRy7:Sy5 :y6 +1; !!VRy7&y8=0:-1;      // y7 - количество страниц втор.навыков
!!VRx1&x1<0:%y7 +y7;                    // корректировка номера страницы для отризательных номеров страниц (PageUp)
!!VRy8:Sx1 %y7 *y6;                     // y8 - номер элемента массива, c которого начинается вывод втор.навыков на страницу
!!re y3/1/y6;                           // отображение страницы навыков в экране героя
  !!VRy9:Sy3 +y8 -1;                    // y9 - номер элемента в массиве страниц
  !!br|y9>=y5/y9>27:;                   // прерывание вывода, если известные втор.навыки закончились
  !!SN:My1/y9/?y2;                      // y2 - номер втор.навыка в проверяемом элементе массива
  !!HE-1:Sy3/y2/1;                      // вывод в слот втор.навыка из массива страниц
!!en:;                                  // 
!!VRy3:-1; !!HE-1:Sy3;                  // ограничение отображаемого кол-ва втор.навыков, кол-вом показанных
!!SN:My1;                               // удаление временного массива страниц втор.навыков

** end