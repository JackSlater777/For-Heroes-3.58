ZVSE2

*** HotA Scouting


!$OB81;                                         // Пост-триггер посещения ученого
!!FU(HotA_CalculateScoutingRadiusAndReveal):P;

!$OB104;                                        // Пост-триггер посещения университета
!!FU(HotA_CalculateScoutingRadiusAndReveal):P;

!$OB113;                                        // Пост-триггер посещения хижины ведьмы
!!FU(HotA_CalculateScoutingRadiusAndReveal):P;

!?FU(OnAfterHeroGainLevel);                     // Пост-триггер лвл-апа
!!FU(HotA_CalculateScoutingRadiusAndReveal):P;

!?FU(OnEquipArt)|v998=(ART_SPECULUM)/v998=(ART_SPYGLASS);// Герой надел телескоп (52) или подзорную трубу (53)
!!FU(HotA_CalculateScoutingRadiusAndReveal):P;

!?FU(OnCloseHeroScreen);                        // Игрок закрыл экран героя
!!FU(HotA_CalculateScoutingRadiusAndReveal):P;

!?FU(OnAfterHeroInteraction);                   // После диалога двух героев
!!FU(HotA_CalculateScoutingRadiusAndReveal):P;

!?FU(HotA_CalculateScoutingRadiusAndReveal);    // Рассчитываем радиус разведки
!!HE-1:P?y1/?y2/?y3;                            // Получаем координаты героя: y1 - X, y2 - Y, y3 - L
!!HE-1:O?y4;                                    // y4 - цвет владельца героя
!!HE-1:S3/?y5;                                  // y5 - уровень разведки героя
!!HE-1:A2/52/d/?y6;                             // y6 - надетый телескоп
!!HE-1:A2/53/k/?y7;                             // y7 - надетая подзорная труба
!!VRy6&v998=52:S1;
!!VRy7&v998=53:S1;
*!HE-1:X?y8/?y9/d/d/d/d/d;                      // y8=0 - спец по вторичному навыку, y9=3 - разведка
*!HE-1&y8=0/y9=3:Ed/?y10;                       // y10 - лвл героя
*!VRy11&y8=0/y9=3:Sy10 :6;                      // Бонус от специализации - лвл поделеный на 6
!!VRy12:S5;                                     // y12 = 5 (базовый радиус разведки)
!!VRy12&y5=1:+1;                                // Базовая разведка +1 радиус
!!VRy12&y5=2:+3;                                // Продвинутая разведка +3 радиус
!!VRy12&y5=3:+5;                                // Экспертная разведка +5 радиус
!!VRy12&y6>0:+1;                                // Надетый телескоп +1 радиус
!!VRy12&y7>0:+1;                                // Надетая подзорная труба +1 радиус
*!VRy12&y8=0/y9=3:+y11;                         // Бонус от специализации
!!UN:Sy1/y2/y3/y4/y12;                          // Открываем вокруг героя y12 квадратов
!!UN:R1;                                        // Обновляем экран

**end
